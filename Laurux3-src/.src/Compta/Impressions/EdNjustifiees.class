' Gambas class file

'----------------------------------------------------------------------------
'

'
'Ce programme est un logiciel libre; Vous pouvez le redistribuer et/ou
'le modifier selon les termes de la GNU General PUBLIC License publiés par
'la Free Software Foundation.

'Ce programme est distribué sans garantie. Voyez la GNU General PUBLIC
'License pour plus de details.

'Vous devez lire la GNU General PUBLIC Licence.
'Si vous ne disposez pas d'un exemplaire, veuillez écrire
'à " The Free Software Foundation
'INC, 59 Temple place - Suite 33, Boston
'MA 02111-1307 USA
'----------------------------------------------------------------------------
'################################################
'# nom du fichier           : EdNjustifiées.class
'# Auteur                   : Jacky Tripoteau
'# date de création         : 01/03/2011
'# Edition des ecritures non justifiées
'################################################

Private TotDeb As Float
Private TotCred As Float
Private TotSld As Float
Private debit As Float
Private credit As Float
Private solde As Float
Private Filename As String
Private Tab1 As String
Private jour As String
Private Compt As String
Private cResult As Result
Private rResult As Result
Private Tab As String
Private son As Integer = Start.Son
Private Coulfond As New String[]
Private PosY As Integer
Private Tri As String
Private pdf As Ccompta
Private Type As String

'*************************
'* On initialise l'écran *
'*************************
Public Sub _New()
  
  Coulfond = Utils.Coulfd()
  If Settings["/Soc" & Start.Societe & "/Coul_fen"] Then Utils.Observers(Me)
  
  Music.Load(Start.Musique)
  Me.Center
  Bclient_Click()
  jour = Format$(Now, "dd.mm.yyyy")
  
End

'***********************
'* On gere nos touches *
'***********************
Public Sub Cmpt_KeyPress()
  
  If Key.code = Key.enter Or Key.code = Key.return Or Key.code = Key.Tab Then
    Select Case Last.tag
        
      Case 1
        'compte du debut
        cpt_LostFocus()
        cpt2_GotFocus()
        cpt2.SetFocus
        cpt2.Select
        Stop Event
        
      Case 2
        'compte de fin
        cpt2_LostFocus()
        cpt_GotFocus()
        cpt.SetFocus
        cpt.Select
        Stop Event
    End Select
  Endif
  
  If key.code = key.F1 Then
    Button3_Click()
    Stop Event
  Endif
  
  If key.code = key.F2 Then
    Select Case Last.tag
      Case 3
        ToggleButton1_click()
        Stop Event
        
      Case 4
        ToggleButton2_click()
        Stop Event
    End Select
  Endif
  
End

'***********************
'* On gere nos boutons *
'***********************
Public Sub Btn_Click()
  
  Select Case Last.tag
      
    Case 1
      
    Case 2
      'On imprime
      Button1_Click()
      
    Case 3
      ' On ferme
      Me.Close
      
  End Select
  
End

'****************************************
'* On gere le focus des zones de saisie *
'****************************************
Public Sub cpt_GotFocus()
  
  Try Utils.ObsGotf(Cpt)
  
End

Public Sub cpt_LostFocus()
  
  Try Utils.ObsLstf(Cpt)
  
End

Public Sub cpt2_GotFocus()
  
  Try Utils.ObsGotf(Cpt2)
  
End

Public Sub cpt2_LostFocus()
  
  Try Utils.ObsLstf(Cpt2)
  
End

'********************
'* On remet à blanc *
'********************
Public Sub Raz()
  
  totdeb = 0
  totcred = 0
  totsld = 0
  solde = 0
  debit = 0
  credit = 0
  solde = 0
  
End

'*********************************
'* On veut les ecritures client *
'*********************************
Public Sub Bclient_Click()
  
  cpt.Text = "41100001"
  cpt2.Text = "41199999"
  type = "C"
  cpt_gotfocus()
  cpt.Setfocus
  
End

'*************************************
'* On veut les ecritures fournisseur*
'*************************************
Public Sub Bfournisseur_Click()
  
  cpt.Text = "40100001"
  cpt2.Text = "40199999"
  type = "F"
  cpt_gotfocus()
  cpt.Setfocus
  
End

'***************************************
'* On selectionne le début de la liste *
'***************************************
Public Sub ToggleButton1_Click()
  
  Dim MyForm As TriComptes
  
  Tri = "cast(compte_cc AS char)"
  MyForm = New TriComptes(Tri, Type, "", "D4", "")
  MyForm.Showmodal()
  If Comptabilite.Bsel = True Then
    Cpt.text = Comptabilite.sCmpt
    Cpt2.SetFocus
  Endif
  
End

'***************************************
'* On selectionne la fin de la liste   *
'***************************************
Public Sub ToggleButton2_Click()
  
  Dim MyForm As TriComptes
  
  Tri = "cast(compte_cc AS char)"
  MyForm = New TriComptes(Tri, Type, "", "D4", "")
  MyForm.Showmodal()
  If Comptabilite.Bsel = True Then
    Cpt2.text = Comptabilite.sCmpt
    Cpt.SetFocus
  Endif
  
End

'******************
'*   On imprime   *
'******************
Public Sub Button1_Click()
  
  Dim i As Integer
  Dim p As Integer = 0
  Dim Intit As String
  Dim dte As String
  Dim dateedition As String
  Dim Tab3 As String
  Dim Ste As String
  Dim SteResult As Result
  Dim SoldeC As Float
  Dim Tot As Float
  
  Tab = "Fiches_Mvt" 
  Tab1 = "Ctrln" 
  Tab3 = "Fiches_Societes"
  SteResult = Utils.db.Exec("SELECT * FROM " & Tab3 & " Where cd_sc = &1", Start.Societe)
  Ste = "Société " & " " & SteResult!int_sc
  Shell "cd " & User.Home & ""
  Filename = User.home & "/Enlettrees.pdf"
  pdf = New Ccompta("Portrait", "mm", "A4")
  pdf.Open()
  pdf.AliasNbPages()
  dateedition = "du " & jour
  pdf.Entete("Impression des écritures non justifiées.")
  pdf.Level0(dateedition, Ste)
  posy = 30
  pdf.Level1()
  posy += 5
  Me.Mouse = Mouse.Wait
  With Utils
    cResult = Utils.db.Exec("SELECT  compte_cc, intitule_cc, solde  FROM " & Cbase.Table("TabComptes") & " WHERE cast(compte_cc as char) >= &1  and cast(compte_cc as char) <= &2  and type_cc =&4 order by cast(compte_cc AS char)", cpt.text, cpt2.text, 0, type)
    If cResult.Available Then
      Repeat
        compt = cResult!compte_cc
        Intit = cResult!intitule_cc
        SoldeC = cResult!solde
        rResult = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabMvt") & " WHERE compte = &1 AND validee = &2 AND lettree = &3 order by dte ", compt, 1, 0)
        If rResult.count <> 0 Then
          Tot = 0
          i = 0
          While i < rResult.count
            Tot = Tot - rResult!montantc + rResult!montantd
            Inc i
            rResult.MoveNext
          Wend
          If Format$(Val(.cpoint(SoldeC)), ",0.00") <> Format$(Val(.cpoint(Tot)), ",0.00") Then
            rResult.MoveFirst()
            i = 0
            If PosY >= 280 Then
              pdf.Entete("Impression des écritures non justifiées.")
              pdf.Level0(dateedition, Ste)
              posy = 30
              pdf.Level1()
              posy += 5
            Endif
            pdf.level6(Compt, Intit, SoldeC, posy)
            posy += 5
            Inc p
            While i < rResult.count
              debit = rResult!montantd
              credit = rResult!montantc
              solde = solde + debit - credit
              totdeb = totdeb + debit
              totcred = totcred + credit
              totsld = totdeb - totcred
              dte = .Cdate_Aff(rResult!dte)
              dte = Left(dte, 6) & Right(dte, 2)
              If PosY >= 280 Then
                pdf.Entete("Impression des écritures non justifiées.")
                pdf.Level0(dateedition, Ste)
                posy = 30
                pdf.Level1()
                posy += 5
              Endif
              pdf.level2(rResult!libelle, rResult!jour, dte, rResult!numdoc, rResult!numlot, debit, credit, solde, Posy)
              posy += 5
              Inc p
              If p >= 42 Then
                p = 2
              Endif
              i = i + 1
              rResult.MoveNext
            Wend
            If PosY >= 280 Then
              pdf.Entete("Impression des écritures non justifiées.")
              pdf.Level0(dateedition, Ste)
              posy = 30
              pdf.Level1()
              posy += 5
            Endif
            posy += 5
            p = p + 2
          Endif
          raz()
          If p >= 42 Then
            p = 2
          Endif
        Endif
      Until cResult.MoveNext()
      posy += 5
      If PosY >= 280 Then
        pdf.Entete("Impression des écritures non justifiées.")
        pdf.Level0(dateedition, Ste)
        posy = 30
        pdf.Level1()
        posy += 5
      Endif
      pdf.BasPage()
      pdf.Output(Filename, False)
      Visualiseur.Prog_Editeur(Filename)
    Else
      If son Then
        Music.Play
      Endif
      message(" Aucune ecriture pour cette demande", "OK")
    Endif
  End With
  Me.Mouse = Mouse.Pointing
  
End

'********************
'* On lance la doc  *
'********************
Public Sub Button3_Click()
  
  Exec ["xdg-open", Application.Path &/ "Ecrans/Njustifiees.html"]
  
End
