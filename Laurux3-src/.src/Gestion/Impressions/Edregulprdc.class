' Gambas class file

'----------------------------------------------------------------------------
'

'
'Ce programme est un logiciel libre; Vous pouvez le redistribuer et/ou
'le modifier selon les termes de la GNU General PUBLIC License publiés par
'la Free Software Foundation.

'Ce programme est distribué sans garantie. Voyez la GNU General PUBLIC
'License pour plus de details.

'Vous devez lire la GNU General PUBLIC Licence.
'Si vous ne disposez pas d'un exemplaire, veuillez écrire
'à " The Free Software Foundation
'INC, 59 Temple place - Suite 33, Boston
'MA 02111-1307 USA
'----------------------------------------------------------------------------
'################################################
'# nom du fichier           : Edregul.class
'# Auteur                   : Jacky Tripoteau
'# date de création         : 16/11/2005
'# Impression des Bl fournisseurs
'################################################
'

Filename As String
jour As String
son As Integer = Start.Son
PosY As Integer
PosX As Integer

Public Sub _New()
  
  If Settings["/Soc" & Start.Societe & "/Coul_fen"] Then Utils.Observers(Me)
  Me.Center
  Music.Load(Start.Musique)
  jour = Format$(Now, "dd.mm.yyyy")
  date2.Text = jour
  date1.Text = Left$(date2.Text, 6) & Str(Val(Right$(date2.Text, 4)) - 4)
  date1.SetFocus
  date1.Select
  
End

'***********************
'* On gere nos touches *
'***********************
Public Sub Cmpt_KeyPress()
  
  Select Case Last.tag
      
    Case 1
      ' date de début
      If Key.code = Key.enter Or Key.code = Key.return Or Key.code = Key.Tab Then
        date1_LostFocus()
        date2.SetFocus
        date2.Select
        Stop Event
      Endif
      
    Case 2
      ' date de fin
      If Key.code = Key.enter Or Key.code = Key.return Or Key.code = Key.Tab Then
        date2_LostFocus()
        date1.SetFocus
        date1.Select
        Stop Event
      Endif
  End Select
  
  If key.code = key.F1 Then
    'Button3_Click()
    Stop Event
  Endif
  
End
'****************************************************
'*      Gestion du focus. Routine de Charlie Reinl  *
'****************************************************

Public Sub Cmpt_GotFocus()
  
  With Utils
    .SetEditColor(Me, Last)
  End With
  
End

Public Sub date1_LostFocus()
  
  With utils
    date1.text = .Cdate_calc(date1.Text)
    date1.Text = .Cdate_aff(date1.Text)
    If date1.Text = "00.00.0:00" Then date1.Text = Format$(Now, "dd.mm.yyyy")
  End With
  date1.Background = &HF9F9BD&
Catch
  date1.SetFocus
  date1.Select
  
End

Public Sub date2_GotFocus()
  
  date2.Background = &HAAFF7F&
  
End

Public Sub date2_LostFocus()
  
  With utils
    date2.text = .Cdate_calc(date2.Text)
    date2.Text = .Cdate_aff(date2.Text)
    If date2.Text = "00.00.0:00" Then date2.Text = Format$(Now, "dd.mm.yyyy")
  End With
  If Val(Right$(date2.text, 4) & Mid$(date2.text, 4, 2) & Left$(date2.text, 2)) < Val(Right$(date1.text, 4) & Mid$(date1.text, 4, 2) & Left$(date1.text, 2)) Then
    If son Then
      Music.Play
    Endif
    If Message.Warning("Attention ! Votre selection n'est pas possible.", "Ok") = 1 Then
    Endif
  Endif
  date2.Background = &HF9F9BD&
Catch
  date2.SetFocus
  date2.Select
  
End

Public Sub Button2_Click()
  
  Dim rResult As Result
  Dim ArtResult As Result
  Dim Tab, Tab2 As String
  Dim p As Integer
  Dim code As String
  Dim intitule As String
  Dim dater As String
  Dim qtep As String
  Dim qtem As String
  Dim comm As String
  Dim dte As String
  Dim Nbpage As Integer
  Dim numero As String
  Dim pdf As Carticles
  
  dte = Format$(Now, "dd.mm.yyyy")
  Tab = "Fiches_Mvtexpdc" 
  Tab2 = "Fiches_Art" 
  With Utils
    Nbpage = 1
    Shell "cd " & User.Home & ""
    Filename = User.home & "/Regul.pdf"
    pdf = New CArticles("Portrait", "mm", "A4")
    pdf.Open()
    pdf.AliasNbPages()
    pdf.Entete("Impression des entrées des produits composés")
    dte = Format$(Now, "dd.mm.yyyy")
    pdf.Level11()
    posx = 40
    posy = 26
    Me.Mouse = Mouse.Wait
    rResult = Utils.db.Exec("SELECT * FROM " & Tab & " WHERE daterecpt >= &1  and daterecpt <= &2 order by mind, type, code", .Cdate_Dbase(date1.Text), .Cdate_Dbase(date2.Text))
    If rResult.Available Then
      Repeat
        If Numero <> rResult!mind And Not IsNull(Numero) Then pdf.Ligne(PosY)
        code = rResult!code
        ArtResult = Utils.db.Exec("SELECT * FROM " & Tab2 & " WHERE art_code= &1", rResult!code)
        Try intitule = ArtResult!art_design
        If Error Then Intitule = "Article inexistant"
        dater = .Cdate_Aff(rResult!daterecpt)
        qtep = rResult!qtep
        qtem = rResult!qtem
        comm = rResult!com
        Inc p
        pdf.level12(code, Left$(intitule, 40), dater, qtep, qtem, comm, PosY)
        PosY += 5
        If PosY >= 283 Then
          pdf.Footer()
          pdf.Entete("Impression des régularisations de stock")
          pdf.Level11()
          PosY = 26
        Endif
        Numero = rResult!mind
      Until rResult.MoveNext()
      pdf.Ligne(PosY)
      pdf.Footer()
      Wait 0.5
      pdf.Output(Filename, False)
      Impression.Prog_Editeur(Filename)
    Else
      If son Then
        Music.Play
      Endif
      message(" Aucun mouvement pour cette demande", "OK")
    Endif
    Me.Mouse = Mouse.Pointing
    If Effmvts.Value = True Then
      If message.Question("Attention, vous allez effacer les entrées des produits composés pour la période demandée !", "Oui", "Non") = 1 Then
        rResult = Utils.db.Exec("SELECT * FROM " & Tab & " WHERE daterecpt >= &1  and daterecpt <= &2 order by mind, code", .Cdate_Dbase(date1.Text), .Cdate_Dbase(date2.Text))
        If rResult.Available Then
          Repeat
            code = rResult!code
            Utils.db.Exec("DELETE FROM " & Tab & " WHERE code = &1", code)
          Until rResult.MoveNext()
        Endif
        message(" Les mouvements ont été effacés.")
      Endif
    Endif
  End With
  
End

'Entete des lignes de detail
Public Sub Level1()
  
  Draw.FillStyle = Fill.Dense12
  Draw.font = font["Bitstream Charter"]
  Draw.Font.Bold = False
  Draw.Font.Size = 9
  Draw.Rect(120, 340, 530, 140)
  Draw.Rect(650, 340, 1800, 140)
  Draw.Rect(2450, 340, 540, 140)
  Draw.Rect(2990, 340, 300, 140)
  Draw.Rect(3290, 340, 300, 140)
  Draw.Rect(3590, 340, 1240, 140)
  Draw.Text("Code", 400, 360)
  Draw.Text("Intitulé", 1560, 360)
  Draw.Text("Date", 2600, 360)
  Draw.Text("Qté +", 3020, 360)
  Draw.Text("Qté -", 3320, 360)
  Draw.Text("commentaire", 4000, 360)
  
End

'Lignes de detail
Public Sub Level2(code As String, intitule As String, dater As String, qtep As String, qtem As String, com As String)
  
  Draw.font = font["Bitstream Charter"]
  Draw.Font.Bold = False
  Draw.Font.Size = 8
  Draw.Text(Code, 110, PosY)
  Draw.Text(Intitule, 660, PosY, 1800, 92, 1)
  Draw.Text(dater, 2560, PosY)
  Draw.Text(qtep, 2990, PosY, 290, 92, 2)
  Draw.Text(qtem, 3290, PosY, 290, 92, 2)
  Draw.Text(com, 3600, PosY)
  
End

Public Sub Button3_Click()
  
  Try Kill "~/Regul.ps"
  Me.Close
  
End
