' Gambas class file

'----------------------------------------------------------------------------
'

'
'Ce programme est un logiciel libre; Vous pouvez le redistribuer et/ou
'le modifier selon les termes de la GNU General PUBLIC License publiés par
'la Free Software Foundation.

'Ce programme est distribué sans garantie. Voyez la GNU General PUBLIC
'License pour plus de details.

'Vous devez lire la GNU General PUBLIC Licence.
'Si vous ne disposez pas d'un exemplaire, veuillez écrire
'à " The Free Software Foundation
'INC, 59 Temple place - Suite 33, Boston
'MA 02111-1307 USA
'----------------------------------------------------------------------------
'################################################
'# nom du fichier           : Fpromo.class
'# Auteur                   : Jacky Tripoteau
'# date de création         : 28/01/2010
'# Gestion de la table des promos
'################################################
'
Tri As String
Promotable[4] As String
rResult As Result
son As Integer = Start.Son
CoulZns As Integer ' Variable pour la couleur du background des zones de saisie
CoulZnaf As Integer ' Variable pour la couleur du background des columnviews
Coulfc As Integer ' Variable pour la couleur du focus
Org As Integer 'flag pour origine ouverture datechooser

Public Sub _New()
  
  Dim Frmt As New String[]
  
  Frmt = Utils.FColr(Settings["/Coul/Znaff"])
  CoulZnaf = Val(Frmt[0])
  Frmt = Utils.FColr(Settings["/Coul/Znss"])
  CoulZns = Val(Frmt[0])
  Frmt = Utils.FColr(Settings["/Coul/Focus"])
  Coulfc = Val(Frmt[0])
  If Settings["/Soc" & Start.Societe & "/Coul_fen"] Then Utils.Observers(Me)
  
  Music.Load(Start.Musique)
  Me.Center
  CO.Text = "Code Promo"
  Inte.Text = "Libellé"
  FO.Text = "Date début"
  FA.Text = "Date fin"
  With ColPromo
    .Columns.count = 4
    .Columns[0].Width = CO.Width
    .Columns[1].Width = Inte.Width
    .Columns[2].Width = FO.Width
    .Columns[3].Width = FA.Width
  End With
  Tri = "code"
  Deb2()
  code.SetFocus
  code.Select
  
End

Public Sub ColPromo_Data(Row As Integer, Column As Integer)
  
  With Utils
    Promotable[0] = "code"
    Promotable[1] = "libelle"
    Promotable[2] = "datedeb"
    Promotable[3] = "datefin"
    Try .rs1.MoveTo(Row)
    Colpromo.data.Text = Str(.rs1[Promotable[Column]])
    If column = 2 Or If column = 3 Then Colpromo.data.Text = Format$(.rs1[Promotable[Column]], "dd.mm.yyyy")
  End With
  
End

'****************************************************
'*      Gestion du focus. Routine de Charlie Reinl  *
'****************************************************
Public Sub Cmpt_GotFocus()
  
  Utils.SetEditColor(Me, Last)
  
End

Public Sub Cmpt_MouseUp()
  
  Select Case Last.tag
      
    Case 3
      ToggleButton1_Click()
      Stop Event
      
    Case 4
      ToggleButton2_Click()
      Stop Event
      
  End Select
  
End

Public Sub Cmpt_Keypress()
  
  If Key.code = Key.enter Or Key.code = Key.return Or Key.code = Key.Tab Then
    Select Case Last.tag
      Case 1
        Desg.SetFocus
        Desg.Select
        Stop Event
        
      Case 2
        Date1.SetFocus
        Date1.Select
        Stop Event
        
      Case 3
        Date2.SetFocus
        Date2.Select
        Stop Event
        
      Case 4
        Code.SetFocus
        Code.Select
        Stop Event
        
    End Select
  Endif
  
End

Public Sub ToggleButton1_Click()
  
  If DateChooser1.Visible = False Then
    DateChooser1.visible = True
  Else
    DateChooser1.Visible = False
  Endif
  Org = 1
  
End

Public Sub ToggleButton2_Click()
  
  If DateChooser1.Visible = False Then
    DateChooser1.visible = True
  Else
    DateChooser1.Visible = False
  Endif
  Org = 2
  
End

Public Sub DateChooser1_Activate()
  
  If Org = 1 Then
    Date1.text = Format$(DateChooser1.Value, "dd.mm.yyyy")
    Date2.SetFocus
    date2.Select
  Else
    Date2.text = Format$(DateChooser1.Value, "dd.mm.yyyy")
    Code.SetFocus
    Code.Select
  Endif
  DateChooser1.Visible = False
  
End

Public Sub Deb2()
  
  Utils.Base(Colpromo, "select * from " & Cbase.Table("TabPromo") & " order by " & Tri & "")
  
End

Public Sub Deb3()
  
  Utils.Base(Colpromo, "select * from " & Cbase.Table("TabPromo") & " order by " & Tri & " Desc")
  
End

Public Sub Button4_Click()
  
  Me.Close
  
End

Public Sub Btn_Click()
  
  Select Case Last.tag
    Case 2
      Button2_Click()
      
    Case 3
      Button3_Click()
      
  End Select
  
End

'**********************************
'*  Enregistrement de la promo    *
'**********************************
Public Sub Button3_Click()
  
  If Not IsNull(code.text) Then
    rResult = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabPromo") & " where code = &1 ", code.Text)
    If Not rResult.Available Then
      Utils.db.Exec("INSERT INTO " & Cbase.Table("TabPromo") & " (code, libelle, datedeb, datefin) VALUES (&1, &2, &3, &4)", code.Text, Desg.Text, Utils.Cdate_Dbase(Date1.Text), Utils.Cdate_Dbase(Date2.Text))
    Else
      Utils.db.Exec("UPdate  " & Cbase.Table("TabPromo") & "  SET  code = &1 , libelle = &2 , datedeb = &3 , datefin = &4 where code = &1", code.Text, Desg.Text, Utils.Cdate_Dbase(Date1.Text), Utils.Cdate_Dbase(Date2.Text))
    Endif
    Tri = "code"
    Deb2()
    CleanChamps()
  Else
    If son Then
      Music.Play
    Endif
    Message.Warning("veuillez saisir un code promotion SVP !")
  Endif
  
End

'**********************************
'* Suppression du code promo      *
'**********************************
Public Sub Button2_Click()
  
  If son Then
    Music.Play
  Endif
  If Not IsNull(code.text) Then
    If Message.Question("Etes vous sur de vouloir effacer cet enregistrement ? \n Plus aucun produit ne sera associé à cette promo. ", "Oui", "Non") = 1 Then
      Me.Mouse = Mouse.wait
      Utils.db.Exec("DELETE FROM " & Cbase.Table("TabArtPromo") & " WHERE code_promo = &1", code.Text)
      Utils.db.Exec("DELETE FROM " & Cbase.Table("TabPromo") & " WHERE code = &1", code.Text)
    Endif
    cleanchamps()
    Me.mouse = mouse.pointing
    Tri = "code"
    Deb2()
  Endif
  
End

Public Sub ColPromo_Click()
  
  CleanChamps()
  Maj_Zones()
  code.SetFocus
  code.Select
  
End

Public Sub CleanChamps()
  
  code.Clear
  Desg.Clear
  Date1.Clear
  Date2.Clear
  
End

Public Sub Maj_Zones()
  
  Dim rpromo As Result
  
  With Utils
    code.Text = ColPromo[ColPromo.row, 0].Text
    rpromo = .db.Exec("select * from " & Cbase.Table("TabPromo") & " where code = &1", Code.Text)
    Desg.text = rpromo!libelle
    Date1.Text = Format$(rpromo!datedeb, "dd.mm.yyyy")
    Date2.Text = Format$(rpromo!datefin, "dd.mm.yyyy")
  End With
Catch
  
End

'********************
'* On lance la doc  *
'********************
Public Sub Button1_Click()
  
  Exec ["xdg-open", Application.Path &/ "Ecrans/Promo.html"]
  
End
