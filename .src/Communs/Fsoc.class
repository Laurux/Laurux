' Gambas class file

' Gambas class file

'----------------------------------------------------------------------------
'

'
'Ce programme est un logiciel libre; Vous pouvez le redistribuer et/ou
'le modifier selon les termes de la GNU General PUBLIC License publiés par
'la Free Software Foundation.

'Ce programme est distribué sans garantie. Voyez la GNU General PUBLIC
'License pour plus de details.

'Vous devez lire la GNU General PUBLIC Licence.
'Si vous ne disposez pas d'un exemplaire, veuillez écrire
'à  " The Free Software Foundation
'INC, 59 Temple place - Suite 33, Boston
'MA 02111-1307 USA
'----------------------------------------------------------------------------
'################################################
'# nom du fichier           : Fsoc.class
'# Auteur                   : Jacky Tripoteau
'# date de création         : 01/11/2004
'# Gestion de la table des societés
'################################################
'

Private Tab As String
Private Soc As String
Private Soc1 As String
Private s As Integer
Private son As Integer = Start.Son
Private testerTVA As Boolean = True
Private cs As String
Private Lig As String
Private chkNbase As Boolean = False

Public Sub _New()

  Utils.Observers(Me)
  Music.Load(Start.Musique)
  Label11.text = Start.LocalSettings["/Soc" & Start.Societe & "/Siret"]
  s = 0
  Me.Center
  Soclist.Columns.count = 3
  Soclist.Columns[0].Width = 65
  Soclist.Columns[1].Width = 320
  Soclist.Columns[2].Width = 0
  Soclist.Columns[0].Text = "Code"
  Soclist.Columns[1].Text = "Intitule"
  Init_base()
  Init_Civilite()
  chkNbase = False
  Recup_Soc()

End

Public Sub Recup_Soc()

  Soclist.MoveFirst()
  Repeat
    Soclist.Item.Selected = True
    If Soclist.Item[0] = Start.Societe Then Break
  Until Soclist.MoveNext()
Catch
  Me.Close

End

Public Sub Form_Open()

  Maj_Zones()
  Code.SetFocus()

Catch
  message.Error(Error.Text & " " & Error.where)

End

Public Sub Init_base()

  Dim rResult As Result
  Dim Tab2 As String
  Dim elements As Variant
  Dim Lig2 As String[]
  Dim bBase As Boolean = False

  Soclist.Clear
  For Each elements In Utils.db.Databases
    Lig = elements.name
    If Start.LocalSettings["/Soc" & Start.Societe & "/Prefixe"] Then
      Lig2 = Split(Lig, "_")
      If lig2.Count = 2 Then
        If Left$(Lig2[1], 6) = "Laurux" Then
          bBase = True
        Else
          bBase = False
        Endif
      Else
        bBase = False
      Endif
    Else
      If Left$(Lig, 6) = "Laurux" And If Len(lig) > 6 Then
        bBase = True
      Else
        bBase = False
      Endif
    Endif
    If bBase Then
      Tab2 = Lig & "." & "Fiches_Societes"
      If Start.LocalSettings["/Soc" & Start.Societe & "/Prefixe"] Then
        Try rResult = Utils.db.Exec("Select * From  " & Tab2 & "")
      Else
        Try rResult = Utils.db.Exec("Select * From  " & Tab2 & " where cd_sc = &1", Right$(Lig, Len(Lig) - 6))
      Endif
      If Not Error Then
        If rResult.Available Then
          Try Soclist.Add(rResult!cd_sc, rResult!cd_sc)
          If Not Error Then
            Soclist.Item[0] = rResult!cd_sc
            Soclist.Item[1] = rResult!type_sc & " " & rResult!int_sc
            Soclist.Item[2] = Lig
          Endif
        Endif
      Endif
    Endif
  Next
Catch
  Message.warning(Error.text)

End

Public Sub Init_Civilite()

  Dim rResult As Result
  ' using Fiches_Identite for ComboBox
  Tab = "Fiches_Identite"
  rResult = Utils.db.Exec("SELECT * FROM " & Tab & "")
  If rResult.Available Then
    Civilite.Clear()
    ' first is an empty
    Civilite.Add("  ", "00")
    Repeat
      Civilite.Add(rResult!libelle, rResult!code)
    Until rResult.MoveNext()
  Endif
Catch

End

Public Function Nsoc(numero As String) As String[]

  Dim spl As New String[2]

  spl = Scan(numero, "*:*")
  Return spl

End

Public Sub Maj_Zones()

  Dim rResult As Result

  Tab = "Fiches_Societes"
  Try Cs = Soclist.Item[0]
  If Not IsNull(cs) Then
    rResult = Utils.db.Exec("SELECT * FROM " & Tab & " where cd_sc = &1", Cs)
    If rResult.Available Then
      'Soclist.Add(rResult!cd_sc, rResult!cd_sc)
      code.Text = rResult!cd_sc
      Civilite.Text = rResult!type_sc
      intitule.Text = rResult!int_sc
      Libre.text = rResult!libre
      adr1.Text = rResult!adr1_sc
      adr2.Text = rResult!adr2_sc
      cpostal.Text = rResult!cp_sc
      Burdis.Text = rResult!burdis_sc
      Email.Text = rResult!email_sc
      Rcs.Text = rResult!rcs_sc
      villercs.Text = rResult!villerc_sc
      Siret.Text = rResult!siret_sc
      Tvaintra.Text = rResult!tvaintra_sc
      Capital.Text = rResult!cap_sc
      Ape.Text = rResult!ape_sc
      Tel.Text = rResult!tel_sc
      Port.Text = rResult!port_sc
      fax.Text = rResult!fax_sc
      site.Text = rResult!site
      Banque.Text = rResult!banq
      Bic.Text = rResult!bic
    Endif
  Endif
Catch

End

Public Sub cleanChamps()

  code.Clear
  Civilite.Text = ""
  intitule.Clear
  Libre.clear
  adr1.Clear
  adr2.Clear
  cpostal.Clear
  Burdis.Clear
  Email.Clear
  Rcs.Clear
  villercs.Clear
  Siret.Clear
  Tvaintra.Clear
  Capital.Clear
  Ape.Clear
  Tel.clear
  Port.Clear
  fax.clear
  site.clear
  Banque.Clear
  Bic.clear

End

Public Sub Refresh()

  Init_base()
  Init_Civilite()
  chkNbase = False
  Recup_Soc()
  Maj_Zones()

End

Public Sub Soc_KeyPress()

  Dim siren As String

  If Key.code = Key.enter Or Key.code = Key.return Or Key.code = Key.Tab Then
    Select Case Last.tag

      Case 1
        code_LostFocus()
        If S <> 1 Then
          Civilite.SetFocus
          Civilite.Select
        Else
          S = 0
          code.SetFocus
          code.Select
        Endif
        Stop Event

      Case 2
        intitule.SetFocus
        intitule.Select
        Stop Event

      Case 3
        adr1.SetFocus
        adr1.Select
        Stop Event

      Case 4
        adr2.SetFocus
        adr2.Select
        Stop Event

      Case 5
        cpostal.SetFocus
        cpostal.Select
        Stop Event

      Case 6
        Burdis.SetFocus
        Burdis.Select
        Stop Event

      Case 7
        Email.SetFocus
        Email.Select
        Stop Event

      Case 8
        Rcs.SetFocus
        Rcs.Select
        Stop Event

      Case 9
        villercs.SetFocus
        villercs.Select
        Stop Event

      Case 10
        Siret.SetFocus
        Siret.Select
        Stop Event

      Case 11
        '##### Récupération du N° intraCommunautaire
        '##### basé sur un calcul
        If Siret.Text <> "" Then
          siren = UCase(Replace(Siret.Text, " ", ""))
          siren = Left$(siren, 9)
          Try Tvaintra.Text = "FR" & CStr(CTestTVA.CleTVAIntra(siren)) & siren
        End If
        '######### FIN #################
        Tvaintra.SetFocus
        Tvaintra.Select
        Stop Event

      Case 12
        '##### Test du Numéro IntraCommunautaire
        If Siret.Text <> "" Then
          siren = UCase(Replace(Siret.Text, " ", ""))
          siren = Left$(siren, 9)
        End If
        If Tvaintra.Text <> "" Then
          testerTVA = CTestTVA.isintracomvalide(Tvaintra.Text, siren)
          If Not testerTVA Then
            Message.Warning("Ce numéro de TVA n'est pas correct pour une société française")
            Stop Event
            'Tvaintra.SetFocus
            'ELSE
            Capital.SetFocus
            Capital.Select
          End If
        End If
        '######### FIN #################
        Stop Event

      Case 13
        Ape.SetFocus
        Ape.Select
        Stop Event

      Case 14
        Tel.SetFocus
        Tel.Select
        Stop Event

      Case 15
        Port.SetFocus
        Port.Select
        Stop Event

      Case 16
        Fax.SetFocus
        fax.Select
        Stop Event

      Case 17
        Site.SetFocus
        Site.Select
        Stop Event

      Case 18
        Banque.SetFocus
        Banque.Select
        Stop Event

      Case 19
        Bic.SetFocus
        Bic.Select
        Stop Event

    End Select
  Endif

  If key.code = key.F1 Then
    Button5_Click()
    Stop Event
  Endif

End

Public Sub Btn_Click()

  Select Case Last.tag

    Case 1
      Button5_Click()

    Case 2
      cleanChamps()
      Code.SetFocus

    Case 3
      Button3_Click()

    Case 4
      Button1_Click()

    Case 5
      Me.Close

    Case 6
      If Start.LocalSettings["/Soc" & Start.Societe & "/Admin"] Then
        MDP.showmodal()
        If Start.Motdepasse = "OK" Then
          Dup.showModal
          Start.Motdepasse = ""
        Endif
      Else
        Dup.showModal
      Endif
      Refresh()

  End Select

End

'****************************************************
'*      Gestion du focus. Routine de Charlie Reinl  *
'****************************************************
Public Sub Soc_GotFocus()

  With Utils
    .SetEditColor(Me, Last)
  End With

End

Public Sub Soc_LostFocus()

  Select Case Last.tag
    Case 8
      If Not IsNull(Email.Text) Then Utils.chkEmail(Email.Text)
  End Select

End

Public Sub code_LostFocus()

  If Len(code.text) = 2 Then
    'code.Background = Color.TextBackground
  Else
    If son Then
      Music.Play
    Endif
    Message.Warning("Le code société doit etre sur 2 caractères !", "OK")
    s = 1
  Endif

End

Public Sub Button1_Click()

  Dim bUpdateOk As Boolean = False
  Dim siren As String
  Dim elements As Variant
  Dim rResult As Result

  Tab = "Fiches_Societes"
  If Siret.Text <> "" Then
    siren = Left$(Siret.Text, 9)
  Endif
  If code.Text <> "" Then
    For Each elements In Utils.db.Databases
      Lig = elements.name
      If Left$(Lig, 6) = "Laurux" Then
        If Len(lig) > 6 Then
          If lig = "Laurux" & Code.Text Then
            bUpdateOk = True
            Break
          Else
            bUpdateOk = False
          Endif
        Endif
      Endif
    Next
    If bUpdateOk = False Then
      If son Then Music.Play
      bUpdateOk = LauruxConf.Createbase("Laurux" & code.Text)
    Endif
    If bUpdateOk Then
      Tab = "Fiches_Societes"
      rResult = Utils.db.Exec("Select * From Laurux" & code.Text & "." & Tab)
      If rResult.Available Then
        Utils.db.Exec("truncate Laurux" & code.Text & "." & Tab)
      Endif
      Utils.db.Exec("Insert Into Laurux" & code.Text & "." & Tab & " (cd_sc, type_sc, int_sc, adr1_sc, adr2_sc, cp_sc, burdis_sc, email_sc, rcs_sc, villerc_sc, siret_sc, tvaintra_sc, cap_sc, ape_sc, tel_sc, port_sc, fax_sc, site, banq, bic, libre)  VALUES (&1,&2,&3,&4,&5,&6,&7,&8,&9,&{10},&{11},&{12},&{13},&{14},&{15},&{16},&{17},&{18},&{19},&{20},&{21})", code.Text, Civilite.Text, intitule.Text, adr1.Text, adr2.Text, cpostal.Text, Burdis.Text, Email.Text, Rcs.Text, villercs.Text, Siret.Text, Tvaintra.Text, Capital.Text, Ape.Text, Tel.Text, Port.Text, Fax.Text, site.Text, Banque.text, Bic.text, Libre.Text)
      bUpdateOk = False
    End If
    Init_base()
    chkNbase = True
    soc = code.Text
    Start.LocalSettings["/Soc"] = soc
    Start.LocalSettings["/intitule"] = intitule.Text
    Start.LocalSettings["/dbase/Name"] = Start.LocalSettings["/dbase/Prefixe"] & "Laurux" & Code.Text
    Start.LocalSettings.Save
    Start.LocalSettings.Reload
    Start.Societe = soc
    Fmenu.text = "Laurux : " & "Société " & Code.Text & " " & Civilite.text & " " & intitule.Text
    If testerTVA Then
      Form_Open()
      code.SetFocus
    Else
      Tvaintra.SetFocus
    Endif
  Endif
Catch
  If son Then
    Music.Play
  Endif
  message.Error(Error.Text & " " & Error.where)
  s = 1

End

Public Sub Button3_Click()

  Dim Tables As String

  Tab = "Fiches_Societes"
  Syntheses.Message("Etes-vous sur de vouloir effacer cette société ?")
  If Message.Question("Etes-vous sur de vouloir effacer cette société", "Oui", "Non") = 1 Then
    Me.mouse = mouse.Wait
    Utils.db.Exec("Drop database Laurux" & code.Text)
    If Not Error Then
      Me.mouse = mouse.Default
      Message.Info("Société " & Tables & " supprimée !\nVeuillez sélectionner une autre société de travail SVP !")
      Start.LocalSettings["Soc"] = ""
      Start.LocalSettings["/Societes/Soc"] = ""
      Start.LocalSettings["/Societes/intitule"] = ""
      Start.LocalSettings.Save
      Start.LocalSettings.Reload
      Init_base()
      cleanChamps()
      Fmenu.Text = ""
      code.SetFocus
    Else
      Me.mouse = mouse.Default
      Message.Info("Suppression impossible ! Il y a un problème sur votre base.")
    Endif
    ' On supprime les tables temporaires si elles existent
    'Try Utils.db.Exec("DROP TABLE Fiches_Inv" & Tables & ",Fiches_InvT" & Tables & ", Fiches_Mats" & Tables & ", Fiches_Regltz" & Tables & ", Fiches_Relcomptes" & Tables & ", Fiches_RelFacture" & Tables & ", Fiches_Stat" & Tables & ", Report" & Tables & ", Detail" & Tables & ", Ecrs" & Tables & ", Ctrln" & Tables & ", Facturefm" & Tables & ", Fiches_Cpostaux" & Tables & " ")
    Me.Raise
  Endif
Catch
  If son Then
    Music.Play
  Endif
  message.Error(Error.Text & " " & Error.where)

End

Public Sub Button4_Click()

  Dim montitre As String

  montitre = "Laurux"
  If code.Text Then
    If Start.LocalSettings["/Soc" & Start.Societe & "/Compta"] Then
      Fmenuc.Text = montitre & " ( Société  " " " & code.Text & " " & intitule.Text & ") "
    Else
      Fmenu.Text = montitre & " ( Société  " " " & code.Text & " " & intitule.Text & ") "
    Endif
    Syntheses.Message(" Vous venez de sélectionner la société " & Soc & " " & intitule.Text)
    message.Warning(" Vous venez de sélectionner la société " & Soc & " " & intitule.Text & " ! \n Celle-ci sera donc votre société de travail par défaut.\n Laurux va être fermé.")
    soc = code.Text
    Start.LocalSettings["/Soc"] = soc
    Start.LocalSettings["/intitule"] = intitule.Text
    Start.LocalSettings["/dbase/Name"] = Start.LocalSettings["/dbase/Prefixe"] & "Laurux" & Code.Text
    Start.LocalSettings.Save
    Start.LocalSettings.Reload
    Utils.db.Close
    Start.Societe = soc
    Utils.db.name = "Laurux" & code.Text
    Utils.db.Open
    Utils.db.exec("SET NAMES 'latin1'")
    If Not IsNull(Utils.db.Login) Then Shell "mysql -h " & Start.LocalSettings["/dbase/Host"] & " " & " -u " & Utils.db.Login & " -p" & Utils.db.Password & " -e \"use " & Utils.db.Name & ";" & "SET @@global.sql_mode= NO_ENGINE_SUBSTITUTION " & "; \"" Wait
    Fmenu.text = "Laurux : " & "Société " & Code.Text & " " & Civilite.text & " " & intitule.Text
    Start.UnLock()
    Me.Close
    If Start.LocalSettings["/Soc" & Soc & "/Compta"] Then
      Fmenuc.Close
    Else
      If Start.LocalSettings["/Soc" & Soc & "/Gestion"] Then
        Fmenuf.Close
      Else
        Fmenu.Close
      Endif
    Endif
  Else
    If son Then
      Music.Play
    Endif
    message.Warning(" Veuillez selectionner une société SVP")
    code.Select
  Endif

End

Public Sub Button5_Click()

  Doc.Open("Societes")

End

Public Sub Nbase_MouseDown()

  ChkNbase = True
  If IsNull(Code.text) Then Soc1 = ""

End

Public Sub Soclist_Click()

  If Soclist.Item[0] <> Start.Societe Then
    Utils.db.Close
    Utils.db.name = Soclist.Item[2]
    Utils.db.Open
    Utils.db.exec("SET NAMES 'latin1'")
    cs = Soclist.Item[0]
    If Not IsNull(Utils.db.Login) Then Shell "mysql -h " & Start.LocalSettings["/dbase/Host"] & " " & " -u " & Utils.db.Login & " -p" & Utils.db.Password & " -e \"use " & Utils.db.Name & ";" & "SET @@global.sql_mode= NO_ENGINE_SUBSTITUTION " & "; \"" Wait
    Maj_Zones()
    soc = code.Text
    Start.LocalSettings["/Soc"] = soc
    Start.LocalSettings["/intitule"] = intitule.Text
    Start.LocalSettings["/dbase/Name"] = Soclist.Item[2]
    Start.LocalSettings.Save
    Start.LocalSettings.Reload
    Start.Societe = soc
    Syntheses.Message(" Vous venez de sélectionner la société " & Soc & " , " & intitule.Text)
    message.Warning(" Vous venez de sélectionner la société " & Soc & " " & intitule.Text & " ! \n Celle-ci sera donc votre société de travail par défaut...\nLaurux va redémarrer")
    chkNbase = False
    Utils.fermeTunnel()
    Utils.db.Close
    Try Me.close
    Try Fmenu.delete
    Try Fmenuc.delete
    Try Fmenuf.delete
    Start.Main()
  Else
    Maj_Zones()
  Endif

End
