' Gambas class file

'----------------------------------------------------------------------------
'

'
'Ce programme est un logiciel libre; Vous pouvez le redistribuer et/ou
'le modifier selon les termes de la GNU General PUBLIC License publiés par
'la Free Software Foundation.

'Ce programme est distribué sans garantie. Voyez la GNU General PUBLIC
'License pour plus de details.

'Vous devez lire la GNU General PUBLIC Licence.
'Si vous ne disposez pas d'un exemplaire, veuillez écrire
'à " The Free Software Foundation
'INC, 59 Temple place - Suite 33, Boston
'MA 02111-1307 USA
'----------------------------------------------------------------------------
'################################################
'# nom du fichier           : Fjournaux.class
'# Auteur                   : Jacky Tripoteau
'# date de création         : 01/11/2004
'# Gestion de la table des journaux
'################################################
'
Private b As Integer ' Flag utilisé pour le focus si la donnée a recuperer existe ou non
Private son As Integer = Start.Son

Public Sub _New()
  
  If Settings["/Soc" & Start.Societe & "/Coul_fen"] Then Utils.Observers(Me)
  Music.Load(Start.Musique)
  Me.Center
  coljo.Columns.count = 2
  coljo.Columns[0].Width = 40
  coljo.Columns[1].Width = 110
  coljo.Columns[0].Text = "code"
  coljo.Columns[1].Text = "libellé"
  Cdj.SetFocus
  
End

Public Sub Form_Open()
  
  Dim rResult As Result
  Dim TAB As String
  
  Tab = "Fiches_Journaux" 
  rResult = Utils.db.Exec("SELECT * FROM " & tab & " order by code_jo")
  If rResult.Available Then
    Repeat
      coljo.Add(rResult!code_jo, rResult!code_jo)
      coljo.Item[1] = rResult!libelle_jo
      coljo.Item[2] = rResult!type_jo
      coljo.Item[3] = rResult!cde_banque
    Until rResult.MoveNext()
    'Maj_Zones()
  Else
  Endif
  
End

Public Sub ColjoRefresh()
  
  coljo.clear
  form_Open()
  
End

Public Sub Maj_Zones()
  
  Cdj.Text = coljo.Current[0]
  Libeljo.text = coljo.current[1]
  typejo.text = coljo.current[2]
  codebq.text = coljo.current[3]
  Cdj.SetFocus
  
End

Public Sub Cleanchamps()
  
  Cdj.Clear
  Libeljo.Clear
  typejo.Clear
  codebq.Clear
  
End

Public Sub Jo_KeyPress()
  
  If Key.code = Key.Enter Or Key.code = Key.return Or Key.code = Key.Tab Then
    
    Select Case Last.tag
        
      Case 1
        If IsNull(Val(Cdj.text)) Then
          Message.Info("Les codes journaux doivent etre saisis en numériques")
          Cdj.Select
          Cdj.SetFocus
        Else
          Libeljo.SetFocus
          Libeljo.Select
        Endif
        Stop Event
        
      Case 2
        typejo.SetFocus
        typejo.Select
        Stop Event
        
      Case 3
        typejo.Text = Upper$(typejo.Text)
        codebq.SetFocus
        codebq.Select
        Stop Event
        
      Case 4
        cptMan()
        If b = 0 Then
          codebq.SetFocus
          codebq.Select
          Stop Event
        Else
          b = 1
          Cdj.SetFocus
          Cdj.Select
        Endif
        Stop Event
    End Select
  Endif
  
  If key.code = key.F1 Then
    Button5_Click()
    Stop Event
  Endif
  
  If Key.code = Key.F2 Then
    Select Case Last.tag
      Case 4
        Button4_Click()
        Stop Event
    End Select
  Endif
  
End

Public Sub Btn_KeyPress()
  
  Select Case Last.tag
      
    Case 3
      If IsNull(Val(Cdj.text)) Then
        Message.Info("Les codes journaux doivent etre saisis en numériques")
        Cdj.Select
        Cdj.SetFocus
      Else
        Button1_Click()
      Endif
      
  End Select
  
End

Public Sub Btn_Click()
  
  Select Case Last.tag
      
    Case 1
      Button5_Click()
      
    Case 2
      Button3_Click()
      
    Case 3
      If IsNull(Val(Cdj.text)) Then
        Message.Info("Les codes journaux doivent etre saisis en numériques")
        Cdj.Select
        Cdj.SetFocus
      Else
        Button1_Click()
      Endif
      
    Case 4
      Me.Close
      
  End Select
  
End

'****************************************************
'*      Gestion du focus. Routine de Charlie Reinl  *
'****************************************************
Public Sub Jo_GotFocus()
  
  With Utils
    .SetEditColor(Me, Last)
  End With
  
End

'************************************
'*   Enregistrement du journal      *
'************************************
Public Sub Button1_Click()
  
  Dim rResult As Result
  Dim TAB As String
  
  Tab = "Fiches_Journaux" 
  cptMan()
  If Cdj.Text <> "" Then
    If codebq.Text <> "" And typejo.Text <> "TR" Then
      If son Then
        Music.Play
      Endif
      Message.Warning("Vous ne devez pas renseigner le code comptable pour un journal qui n'est pas de type TR", "OK")
    Else
      If codebq.Text = "" And typejo.Text = "TR" Then
        If son Then
          Music.Play
        Endif
        Message.Question("Vous devez renseigner le code comptable pour un journal de type TR", "OK")
      Else
        rResult = Utils.db.Exec("SELECT * FROM " & tab & " WHERE code_jo = '" & Cdj.Text & "'")
        If rResult.Available Then
          Try Utils.db.Exec("UPdate  " & tab & "  SET  code_jo = '" & Cdj.Text & "' , libelle_jo = '" & Libeljo.Text & "' , type_jo = '" & typejo.Text & "', cde_banque = '" & codebq.Text & "'WHERE code_jo = '" & coljo.Current.key & "'")
        Else
          Try Utils.db.Exec("INSERT INTO " & tab & "(code_jo,libelle_jo,type_jo,cde_banque) VALUES ('" & Cdj.Text & "', '" & Libeljo.Text & "', '" & typejo.Text & "', '" & codebq.Text & "')")
        Endif
      Endif
    Endif
    ColjoRefresh()
    Cdj.SetFocus
    Cdj.Select
  Else
    If son Then
      Music.Play
    Endif
    If Message.Question("VOUS DEVEZ D'ABORD REMPLIR LES CHAMPS OBLIGATOIRES", "OK") = 1 Then
      ColjoRefresh()
      Cdj.SetFocus
      Cdj.Select
    Endif
  Endif
  
End

'******************************
'* Selection du journal       *
'******************************
Public Sub Coljo_Click()
  
  Maj_Zones
  
End

'*********************************
'*   Suppression du journal      *
'*********************************
Public Sub Button3_Click()
  
  Dim TAB As String
  
  Tab = "Fiches_Journaux" 
  If son Then
    Music.Play
  Endif
  If Message.Question("Etes vous sur de vouloir effacer cet enregistrement", "Oui", "Non") = 1 Then
    Utils.db.Exec("DELETE FROM " & tab & " WHERE code_jo = '" & coljo.Current[0] & "'")
    ColjoRefresh()
  Endif
  Cdj.SetFocus
  Cdj.Select
  
End

'**********************************************************
'*      Appel du columnview Coljo2 avec le bouton         *
'**********************************************************
Public Sub Button4_Click()
  
  Dim rResult As Result
  Dim TAB As String
  
  Tab = "Fiches_Comptes" 
  If Coljo2.visible Then
    Coljo2.clear
    Coljo2.visible = False
  Else
    Coljo2.visible = True
    Coljo2.Columns.count = 2
    Coljo2.Columns[0].Width = 65
    Coljo2.Columns[1].Width = 180
    Coljo2.Columns[0].Text = "code"
    Coljo2.Columns[1].Text = "Intitulé"
    rResult = Utils.db.Exec("SELECT * FROM " & tab & " where comptr_cc <> &1 order by cast(compte_cc AS char)", 0)
    If rResult.Available Then
      Repeat
        Coljo2.Add(rResult!compte_cc, rResult!compte_cc)
        Coljo2.Item[0] = rResult!compte_cc
        Coljo2.Item[1] = rResult!intitule_cc
      Until rResult.MoveNext()
      Coljo2.MoveFirst
      Coljo2.SetFocus
      Coljo2.Item.Selected = True
    Endif
  Endif
  
End

'**********************************************************
'*       Gestion du columnview Coljo2 avec la souris      *
'**********************************************************
Public Sub Coljo2_Click()
  
  codebq.text = Coljo2.Item[0]
  Coljo2.visible = False
  Coljo2.clear
  Button1.SetFocus
  
End

'***********************************************************
'* Gestion du columnview Coljo2 lors d'une saisie manuelle *
'***********************************************************
Public Sub Coljo2_KeyPress()
  
  If Key.code = Key.Enter Or Key.code = Key.Return Then
    Coljo2.MoveCurrent
    Coljo2_Click()
    Stop Event
  Endif
  
  If key.code = key.F1 Then
    Button5_Click()
    Stop Event
  Endif
  
  If Key.code = Key.Escape Or Key.code = Key.F2 Then
    Coljo2.visible = False
    Coljo2.clear
    codebq.SetFocus
    codebq.Select
    Stop Event
  Endif
  
End

'***********************************
'*    Appel manuel du compte       *
'***********************************
Public Sub cptman()
  
  Dim rResult As Result
  Dim Tab As String
  
  Tab = "Fiches_Comptes" 
  If typejo.Text = "TR" And codebq.Text <> "" Then
    rResult = Utils.db.Exec("SELECT * FROM " & Tab & " where compte_cc = &1", codebq.Text)
    If rResult.Available Then
      codebq.Text = rResult!compte_cc
      b = 1
    Else
      b = 0
      If son Then
        Music.Play
      Endif
      Message.Warning("Attention ! Ce compte n'existe pas.", "OK")
    Endif
  Else
    If typejo.Text <> "TR" And codebq.Text <> "" Then
      If son Then
        Music.Play
      Endif
      Message.Warning("Attention ! Vous ne devez pas renseigner de contre-partie si votre journal n'est pas de type 'TR'.", "OK")
    Endif
    b = 1
  Endif
  
End

'**************************
'*   appel de la doc      *
'**************************
Public Sub Button5_Click()
  
  Exec ["xdg-open", Application.Path &/ "Ecrans/Journaux.html"]
  
End
