' Gambas class file

Private $blargeur As Boolean = True
Private $icompteur As Integer
Private Devises As String
Private son As Integer = Start.Son
Private Coulfd As New String[]
Private Coulfond As String

Public Sub _New()

  Dim rParam As Result

  Utils.Observers(Me)
  Music.Load(Start.Musique)
  Me.Center
  Coulfd = Utils.FColr(Start.LocalSettings["/Coul/Focus"])
  Coulfond = Val(Coulfd[0])
  dteN1.Text = Format$(Now, "dd.mm.yyyy")
  rParam = Utils.db.Exec("SELECT * FROM " & Cbase.Table("TabParam") & "")
  If rParam.Available Then
    devises = rParam!devise
    If IsNull(devises) Then devises = "Euros"
  Endif
  If Devises = "Euros" Then
    devises = "€"
  Else
    Devises = Left(Devises, 1)
  Endif

  Me.Arrangement = Arrange.Vertical
  Me.AutoResize = True
  ScrollView2.w = 1024
  affiche()
  Checktout_Click
  $icompteur = 1
  lig_GotF(0)
  dteN1_Gotfocus()

End

Public Sub lig_GotF(nlig As Integer)

  Dim tab As Variant
  Dim i As Integer

  If $icompteur = nlig Then Return
  tab = ScrollView1.Children
  For i = 0 To tab.count - 1
    tab[i].couleur(Me.Background)
  Next
  Try tab[nlig].couleur(Coulfond)
  Try tab[nlig].visext
  CheckBox2.Value = False
  $icompteur = nlig

End

Public Sub Lig_up(nlig As Integer)

  Dim tab As Variant
  Dim i As Integer

  If nlig = 0 Then Return
  tab = ScrollView1.Children
  For i = 0 To tab.count - 1
    tab[i].couleur(Me.Background)
  Next
  $icompteur -= 1
  tab[$icompteur].couleur(Coulfond)
  ' tab[$icompteur].couleur(&H00FFFF)
  tab[$icompteur].visext
  CheckBox2.Value = False

End

Public Sub lig_Down(nlig As Integer)

  Dim tab As Variant
  Dim i As Integer

  tab = ScrollView1.Children
  If nlig = tab.count - 1 Then Return
  For i = 0 To tab.count - 1
    tab[i].couleur(Me.Background)
  Next
  $icompteur += 1
  ' tab[$icompteur].couleur(&H00FFFF)
  tab[$icompteur].couleur(Coulfond)
  tab[$icompteur].visext
  CheckBox2.Value = False

End

Public Sub ScrollView1_KeyPress()

  If Key.code = Key.Up Then Lig_up($icompteur)
  If Key.Code = Key.Down Then lig_Down($icompteur)

End

Public Sub Checktout_Click()

  Dim tab As Variant
  Dim i As Integer
  Dim sel As Boolean

  If Checktout.Value Then
    sel = True
  Else
    sel = False
  Endif
  tab = ScrollView1.Children
  For i = 0 To tab.count - 1
    tab[i].selection = sel
  Next
  Try tab[$icompteur].setfocus

End

Public Sub CheckBox2_Click()

  Dim tab As Variant
  Dim i As Integer

  tab = ScrollView2.Children
  For i = 0 To tab.count - 1
    tab[i].selection = CheckBox2.Value
  Next

End

Private Sub affiche()

  Dim res As Result
  Dim i As Integer
  Dim lig As Ligcli
  Dim Totc As Float

  res = Utils.db.Exec("SELECT compte FROM Fiches_relances GROUP BY compte ORDER BY compte ASC")
  If res.Available Then
    res.MoveFirst
    Totc = 0
    For i = 0 To res.Max
      lig = New Ligcli(res!compte, i, Devises, ScrollView2, ScrollView1) As "lig"
      lig.Show
      Totc += lig.sold
      res.MoveNext
    Next
    Label1.Text = " " & res.count & " clients vous doivent un total de " & Format(Totc, "0.00 " & devises)
    If $blargeur Then
      ScrollView1.Width = lig.Width + 30
      $blargeur = False
    Endif
  Endif

End

Private Sub efface()

  Dim tab As Variant
  Dim i As Integer

  tab = ScrollView1.Children
  If tab.count = 0 Then Return
  Repeat
    i = tab.count - 1
    tab[i].Delete
  Until i = 0
  ScrollView1.Refresh

End

Public Sub Buttontrnom_Click()

  Dim tab As Variant
  Dim i, j As Integer

  tab = ScrollView1.Children
  j = Buttontrnom.Tag
  Select Case j

    Case 2
      Buttontrnom.Tag = 1
      Buttontrnom.Picture = Picture["Icones/down2.png"]
      For i = 0 To tab.count - 1
        j = i
        Repeat
          If tab[i].numero > tab[j].numero Then
            tab[j].next = tab[i]
          Endif
          Inc j
        Until j >= tab.count
      Next

    Case 1
      Buttontrnom.Tag = 2
      Buttontrnom.Picture = Picture["Icones/up2.png"]
      For i = 0 To tab.count - 1
        j = i
        Repeat
          If tab[i].numero < tab[j].numero Then
            tab[j].next = tab[i]
          Endif
          Inc j
        Until j >= tab.count
      Next

  End Select

  ligtag
  'selcouleur
  'tab[$icompteur].couleur(&H00FFFF)
  ScrollView1.Refresh
  Wait 0.01

End

Public Sub Buttontrn_Click()

  Dim tab As Variant
  Dim i, j As Integer

  tab = ScrollView1.Children
  j = Buttontrn.Tag
  Select Case j

    Case 1
      Buttontrn.Tag = 2
      Buttontrn.Picture = Picture["Icones/down2.png"]
      For i = 0 To tab.count - 1
        j = i
        Repeat
          If tab[i].lib < tab[j].lib Then
            tab[j].next = tab[i]
          Endif
          Inc j
        Until j >= tab.count
      Next

    Case 2
      Buttontrn.Tag = 1
      Buttontrn.Picture = Picture["Icones/up2.png"]
      For i = 0 To tab.count - 1
        j = i
        Repeat
          If tab[i].lib > tab[j].lib Then
            tab[j].next = tab[i]
          Endif
          Inc j
        Until j >= tab.count
      Next

  End Select

  ' selcouleur
  ligtag
  ' tab[$icompteur].couleur(&H00FFFF)
  ScrollView1.Refresh
  Wait 0.01

End

Private Sub ligtag()

  Dim Tab As Variant
  Dim i As Integer

  tab = ScrollView1.Children
  For i = 0 To tab.count - 1
    tab[i].tag = i
  Next

End

Private Sub selcouleur()

  Dim tab As Variant
  Dim i As Integer

  tab = ScrollView1.Children
  For i = 0 To tab.count - 1
    tab[i].couleur(couleur(i))
  Next

End

Private Function couleur(coul As Integer) As Integer

  If Even(coul) Then
    Return Coulfond
  Else
    Return Color.Background
  Endif

End

Public Sub Button1_Click()

  Dim rResult As Result
  Dim rrResult As Result
  Dim Tab As String
  Dim Tab2 As String
  Dim Tab3 As String
  Dim dteN As String
  Dim Niv As Integer

  Tab = "Fiches_Mvt"
  Tab2 = "Fiches_relances"
  Tab3 = "Fiches_Cli"
  '  relances.clear
  '  compteRel.Clear
  Label1.Text = ""
  ' TextLabel2.Text = ""
  Utils.db.Exec("DROP TABLE if exists " & Tab2 & "")
  Utils.db.EXEC("CREATE TABLE " & Tab2 &
    " (compte Char(8) NOT NULL," &
    "rs char(10) ," &
    "intitule char(50) ," &
    "adr1 char(35) ," &
    "adr2 char(35) ," &
    "cp char(10) ," &
    "ville char(35) ," &
    "pays char(35) ," &
    "dte date ," &
    "numdoc char(10)," &
    "numlot char(10) ," &
    "libelle char(50) ," &
    "montantd Decimal(12,2) ," &
    "montantc Decimal(12,2) ," &
    "numero char(8) ," &
    "journal char(2) ," &
    "niveau integer(1)," &
    "lind char(10) ," &
    "PRIMARY KEY (compte, lind))" & Utils.db.Engine())
  If dteN1.Text <> "" Then
    dteN = Right$(dteN1.Text, 4) & Mid$(dteN1.Text, 4, 2) & Left$(dteN1.Text, 2)
    rResult = Utils.db.Exec("SELECT * FROM " & tab & " where left(compte,3) = &1 and dateech <= &2 and tresorerie = &3 and collectif is null and lettree = &4 or left(compte,3) = &1 and dateech is null and tresorerie = &3 and collectif is null and lettree = &4  or left(compte,3) = &1 and dte <= &2 and tresorerie = &5 and collectif is null and lettree = &4", "411", dteN, 0, 0, 1)
    If rResult.Available Then
      Me.mouse = Mouse.Wait
      Repeat
        If rResult!relance = "" Then
          Niv = 1
        Else
          If rResult!relance < 3 Then
            Niv = rResult!relance + 1
          Else
            Niv = 3
          Endif
        Endif
        rrResult = Utils.db.Exec("SELECT * from " & Tab3 & " where cli_code = &1", rResult!compte)
        If rrResult.Available Then
          Try Utils.db.Exec("INSERT INTO " & Tab2 & " (compte, rs, intitule, adr1, adr2, cp, ville, pays, dte, numdoc, numlot, libelle, montantd, montantc, numero, journal, niveau, lind) VALUES (&1, &2, &3, &4, &5, &6, &7, &8, &9, &{10}, &{11}, &{12}, &{13}, &{14}, &{15}, &{16}, &{17}, &{18})", rResult!compte, rrResult!cli_rs_soc, rrResult!cli_nom & " " & rrResult!cli_pnm, rrResult!cli_adr1, rrResult!cli_adr2, rrResult!cli_cd_ptl, rrResult!cli_ville, rrResult!cli_pays, rResult!dateech, rResult!numdoc, rResult!numlot, rResult!libelle, rResult!montantd, rResult!montantc, rResult!numero, rResult!jour, Niv, rResult!lind)
          'If Error Then Message.Error("Il y a un problème sur l'écriture n° " & rResult!numero & " Veuillez vérifier que le numéro de document existe bien !")
          If Error Then Utils.db.Exec("INSERT INTO " & Tab2 & " (compte, rs, intitule, adr1, adr2, cp, ville, pays, dte, numdoc, numlot, libelle, montantd, montantc, numero, journal, niveau, lind) VALUES (&1, &2, &3, &4, &5, &6, &7, &8, &9, &{10}, &{11}, &{12}, &{13}, &{14}, &{15}, &{16}, &{17}, &{18})", rResult!compte, rrResult!cli_rs_soc, rrResult!cli_nom & " " & rrResult!cli_pnm, rrResult!cli_adr1, rrResult!cli_adr2, rrResult!cli_cd_ptl, rrResult!cli_ville, rrResult!cli_pays, rResult!dateech, rResult!compte & rResult!lind, rResult!numlot, rResult!libelle, rResult!montantd, rResult!montantc, rResult!numero, rResult!jour, Niv, rResult!lind)
        Endif
      Until rResult.MoveNext()
      efface
      affiche()
      $icompteur = 0
      Checktout.Value = True
      Checktout_Click
      $icompteur = 1
      lig_GotF(0)

      Me.mouse = Mouse.Default
    Endif
  Else
    If son Then
      Music.Play
    Endif
    Message.Info("Veuillez saisir votre date SVP")
    dteN1_GotFocus()
    dteN1.SetFocus
  Endif

End

Public Sub dte_KeyPress()

  If Key.code = Key.Enter Or Key.code = Key.return Or Key.code = Key.Tab Then
    Select Case Last.tag

      Case 1
        dteN1_LostFocus()
        Stop Event
    End Select

  Endif
  '  If key.code = key.F1 Then
  '    Button7_Click()
  '    Stop Event
  '  Endif

End

Public Sub dteN1_GotFocus()

  Utils.SetEditColor(Me, dteN1)
  dteN1.SetFocus
  dteN1.Select

End

Public Sub dteN1_LostFocus()

  With utils
    dteN1.Text = .Cdate_calc(dteN1.text)
    dteN1.Text = .Cdate_aff(dteN1.Text)
    If dteN1.text = "00.00.0:00" Then
      dteN1.text = Format$(Now, "dd.mm.yyyy")
    Else
      Button1.SetFocus
    Endif
    dteN1.Background = Color.TextBackground
  End With

End

Public Sub Impressionr(Org As String)

  Dim rRESULT As RESULT
  Dim rrRESULT As RESULT
  Dim Tab As String
  Dim Tab2 As String
  Dim Tab3 As String
  Dim Tab4 As String
  Dim adr1 As String
  Dim adr2 As String
  Dim adr21 As String
  Dim adr22 As String
  Dim ville As String
  Dim ville2 As String
  Dim Pays As String
  Dim Mail As String
  Dim Rcs As String
  Dim Siret As String
  Dim Tvaintra As String
  Dim Cap As String
  Dim Ape As String
  Dim Iban As String
  Dim Tel As String
  Dim portable As String
  Dim Fax As String
  Dim Site As String
  Dim Rs As String
  Dim Rs2 As String
  Dim Niv As String
  Dim Txt1 As String
  Dim Txt2 As String
  Dim dte As String
  Dim num As String
  Dim Lib As String
  Dim Mtd As String
  Dim Mtc As String
  Dim jour As String
  Dim pdf As Creleve
  Dim totsel As Float
  Dim res As Result
  Dim Filename As String
  Dim Filetxt As String
  Dim PosY As Integer
  Dim tabl As Variant
  Dim i As Integer

  Tab = "Fiches_relances"
  Tab2 = "Fiches_Societes"
  Tab3 = "Fiches_Txt_Relances"
  Tab4 = "Fiches_Mvt"
  filetxt = User.home & "/Relances.txt"
  Me.Mouse = Mouse.wait
  jour = Format$(Now, "dd.mm.yyyy")
  Filename = User.home & "/tmp/Relances.pdf"
  With utils
    Shell "cd " & User.Home & ""
    pdf = New Creleve("Portrait", "mm", "A4")
    pdf.Open()
    pdf.AliasNbPages()
    rResult = Utils.db.Exec("SELECT * FROM " & tab2 & " where cd_sc = &1", Start.Societe)
    Rs = rResult!type_sc & " " & rResult!int_sc
    adr1 = rResult!adr1_sc
    adr2 = rResult!adr2_sc
    ville = rResult!cp_sc & "  " & rResult!burdis_sc
    jour = rResult!burdis_sc & "  le  " & jour
    If rResult!villerc_sc Then Rcs = "Rcs : " & rResult!rcs_sc & " " & rResult!villerc_sc
    If rResult!siret_sc Then Siret = Start.LocalSettings["/Soc" & Start.Societe & "/Siret"] & " : " & rResult!siret_sc
    If rResult!tvaintra_sc Then Tvaintra = "Tva intra : " & rResult!tvaintra_sc
    If rResult!cap_sc Then Cap = "Capital : " & rResult!cap_sc
    If rResult!ape_sc Then Ape = "Naf : " & rResult!ape_sc
    If rResult!tel_sc Then Tel = "Tel : " & rResult!tel_sc
    If rResult!port_sc Then Portable = "Portable : " & rResult!port_sc
    If rResult!fax_sc Then Fax = "Fax : " & rResult!fax_sc
    If rResult!email_sc Then Mail = "Courriel : " & rResult!email_sc
    If rResult!site Then Site = "Site : " & rResult!site
    If rResult!banq Then Iban = "Iban : " & rResult!banq & " Bic : " & rResult!bic

    '   Edition des lettres de relance
    i = 0
    tabl = ScrollView1.Children
    Repeat
      If tabl[i].selection And tabl[i].imp And Not tabl[i].traitement Then
        rResult = Utils.db.Exec("SELECT * FROM " & tab & " where compte = &1 order by dte", tabl[i].numero)
        If rResult.Available Then
          Rs2 = rResult!rs & " " & rResult!intitule
          adr21 = rResult!adr1
          adr22 = rResult!adr2
          ville2 = rResult!cp & "  " & rResult!ville
          Pays = rResult!pays
          If Pays = "France" Then Pays = ""
          Niv = rResult!niveau
          pdf.Entete(rs, adr1, adr2, ville, cap, rcs, siret, ape, Tvaintra, Tel, portable, fax, mail, site, Pays, Iban, False)
          pdf.Entete2(rs2, adr21, adr22, ville2, Pays, "", False)
          pdf.Level5(Jour)
          posy = 110
          If tabl[i].rellet Then
            rrResult = Utils.db.Exec("select * from " & Tab3 & " where niveau = &1", tabl[i].numlet)
            If rrResult.Available Then
              Txt1 = rrResult!libell1
              Txt2 = rrResult!libell2
            Endif
          Else
            Txt1 = "          Relevé de compte"
            txt2 = "  "
          Endif
          pdf.Level8(Txt1, Posy)
          Posy += 30
          pdf.Level6()
          posy += 6
          totsel = 0
          Repeat
            dte = .Cdate_Aff(rResult!dte)
            num = rResult!numdoc
            Lib = rResult!libelle
            Mtd = Format$(rResult!montantd, "0.00")
            Mtc = Format$(rResult!montantc, "0.00")
            totsel += Val(mtd) - Val(Mtc)
            pdf.Level7(dte, num, Lib, Mtd, Mtc, PosY)
            posy += 4
            Utils.db.Exec("UPdate  " & Tab4 & "  SET  relance = &1 WHERE compte = &2 and numdoc = &3", Niv, tabl[i].numero, num)
            If PosY >= 273 Then
              pdf.Entete(rs, adr1, adr2, ville, cap, rcs, siret, ape, Tvaintra, Tel, portable, fax, mail, site, Pays, Iban, False)
              pdf.Entete2(rs2, adr21, adr22, ville2, Pays, "", False)
              pdf.Level5(Jour)
              posy = 110
              pdf.Level8(Txt1, Posy)
              Posy += 30
              pdf.Level6()
              posy += 6
            Endif
          Until rResult.MoveNext()

          Posy += 6
          If totsel > 0 Then
            pdf.Level7(" ", " ", "Solde de la sélection ", " ", Format(totsel, "0.00"), PosY)
          Else
            pdf.Level7(" ", " ", "Solde de la sélection ", Format(- totsel, "0.00"), " ", PosY)
          Endif
          posy += 4
          res = utils.db.Exec("SELECT solde FROM Fiches_Comptes WHERE compte_cc=&1", tabl[i].numero)
          If res.Available Then

            If Val(.cpoint(res!solde)) > 0 Then
              pdf.Level7(" ", " ", "Solde du compte ", " ", Format(res!solde, "0.00"), PosY)
            Else
              pdf.Level7(" ", " ", "Solde du compte ", Format(- res!solde, "0.00"), " ", PosY)
            Endif

          Endif
          posy += 8
          pdf.Level8(Txt2, Posy)
        Endif
      Endif
      Inc i
    Until i = tabl.count
    pdf.Output(Filename, False)
    Impression.Prog_Editeur(Filename)
    ' Endif
  End With

  '     envoie des mails
  '  i = 0
  '  Repeat
  ' If tabl[i].mail And Not tabl[i].traitement Then

  ' EnvMail.Envoi2(MailC, "Votre compte", "", Filename)
  ' Endif
  '  Inc i
  '  Until i = tabl.count - 1

  Me.Mouse = Mouse.Custom

End

Public Sub Buttonedit_Click()

  Impressionr("")

End

Public Sub Button6_Click()

  Me.Close

End
