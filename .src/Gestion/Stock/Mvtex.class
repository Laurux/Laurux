' Gambas class file

'

'
'Ce programme est un logiciel libre; Vous pouvez le redistribuer et/ou
'le modifier selon les termes de la GNU General PUBLIC License publiés par
'la Free Software Foundation.

'Ce programme est distribué sans garantie. Voyez la GNU General PUBLIC
'License pour plus de details.

'Vous devez lire la GNU General PUBLIC Licence.
'Si vous ne disposez pas d'un exemplaire, veuillez écrire
'à " The Free Software Foundation
'INC, 59 Temple place - Suite 33, Boston
'MA 02111-1307 USA
'----------------------------------------------------------------------------
'################################################
'# nom du fichier           : Receipt.class
'# Auteur                   : Jacky Tripoteau
'# date de création         : 10/10/2005
'# Régularistaion du stock
'################################################
'

B As Integer ' flag pour gestion du champ
Tab As String
Tab2 As String
Decm As String ' nombre de décimales
Stocke As Boolean ' Si article est stocké ?
p As Float
son As Integer = Start.Son
Tri As String
Private $soption As String
Public Codearticle As String
Public Bsel As Boolean = False

Public Sub _New()

  Dim $ch As Chainage
  
  Utils.Observers(Me)
  Music.Load(Start.Musique)
  Me.center
  b = 1
  p = 1
  Colrcp.Columns.count = 5
  Colrcp.Columns[0].Width = 120
  Colrcp.Columns[1].Width = 280
  Colrcp.Columns[2].Width = 80
  Colrcp.Columns[3].Width = 80
  Colrcp.Columns[4].Width = 270
  Colrcp.Columns[0].Text = "Code"
  Colrcp.Columns[0].Alignment = 4
  Colrcp.Columns[1].Text = "Designation"
  Colrcp.Columns[1].Alignment = 4
  Colrcp.Columns[2].Text = "Qté entrée"
  Colrcp.Columns[2].Alignment = 2
  Colrcp.Columns[3].Text = "Qté sortie"
  Colrcp.Columns[3].Alignment = 2
  Colrcp.Columns[4].Text = "Commentaire"
  Colrcp.Columns[4].Alignment = 1
  datej.Dat = True
  datej.dte.G = Now
  Qterecue.numerique = True
  $ch = New Chainage([datej, Cart, Dart, qterecue, Comment, Button1])
  $soption = utils.Option()
  
End

'**********************************************************
'*        On remet a blanc les zones de saisies           *
'**********************************************************
Public Sub CleanChamps()

  Cart.Clear
  Dart.Clear
  Artstock.Clear
  qterecue.Clear

End

'**********************************************************
'*        On remet a blanc toute les zones de l'écran     *
'**********************************************************
Public Sub RazBl()

  CleanChamps()
  Comment.clear
  Colrcp.Clear

End

'**********************************************************
'*      Gestion des touches sur le panel Entete           *
'**********************************************************
Public Sub Rcpt_lostfocus()

    Select Case Last.tag

      Case 2
        If Not IsNull(Cart.Text) Then
          If Left$(Cart.Text) = "*" Then
            Cart.Text = Mid$(Cart.Text, 2, Len(Cart.Text))
            Cart.Text = Utils.Find_cbarre(Cart.Text)
            art_Man()
          Else
            art_Man()
          Endif
        Endif
        Stop Event

      Case 4
        qterecue_LF()
  
    End Select
End

Public Sub Rcpt_gotfocus()

  If Last.tag <> 2 And Last.tag <> 1 Then
    If Cart.Text = "" Then
          If son Then
            Music.Play
          Endif
          Message.Warning("Veuillez saisir un article SVP", "Ok")
          Stop Event
          Cart.SetFocus
          Cart.Select
    Endif
  Endif

End
Public Sub rcppt_keypress()

  If key.code = key.F1 Then
    Button4_Click()
    Stop Event
  Endif

  If key.code = key.F2 Then
    Select Case Last.tag
      Case 2
        ToggleButton2_Click()
        Stop Event
  
    End Select
  Endif
End

'*****************************************************
'*      Gestion des touches sur les boutons          *
'*****************************************************
Public Sub Btn_KeyPress()

  If Key.code = Key.enter Or Key.code = Key.return Then

    Select Case Last.tag

      Case 1
        If Cart.Text <> "" Then
          datej.Enabled = False
          qterecue_LF()
          Maj_colrcp()
          CleanChamps()
          Cart.SetFocus
          Cart.Select
        Endif
        Stop Event

    End Select
  Endif

End

Public Sub Btn_Click()

  Select Case Last.tag

    Case 1
      If Cart.Text <> "" Then
        datej.Enabled = False
        qterecue_LF()
        Maj_colrcp()
        CleanChamps()
        Cart.SetFocus
        Cart.Select
      Endif
      Stop Event

    Case 2
      If Colrcp.Count Then
        Enrgbl()
      Endif
      datej.Enabled = True
      datej.SetFocus
      datej.Select
      Stop Event

    Case 3
      Me.Close

  End Select

End

Public Sub form_close()

  If Colrcp.Count Then
    If Message.Question("Voulez-vous valider la saisie ?", "Oui", "Non") = 1 Then Enrgbl()
  Endif

End


'********************************************
'* Saisie d'un article manuellement         *
'********************************************
Public Sub art_man()

  Dim rResult As Result

  Tab = "Fiches_Art"
  Tab2 = "Fiches_Tvaav"
  rResult = Utils.db.Exec("SELECT * FROM " & Tab & " where art_code = &1", Cart.Text)
  If rResult.Available Then
    With utils
      Stocke = rResult!art_stocke
      If Stocke Then
        B = 0
        Cart.Text = rResult!art_code
        Dart.text = rResult!art_design
        Decm = rResult!art_dec
        Artstock.text = .cpoint(rResult!art_qte)
        qterecue.Text = "1"
        qterecue.nbdec = rResult!art_dec
      Else
        If son Then
          Music.Play
        Endif
        Message.Warning("Cet article n'est pas stocké ! ")
        Cart.Text = ""
        Cart.SetFocus
        Cart.Select
      Endif
    End With
  Else
    If son Then
      Music.Play
    Endif
    message.Warning(" Cet article n'existe pas ! ")
    Cart.SetFocus
    Cart.Select
  Endif

End

Public Sub SelectionArt()

  Dim sel As String = ""
  Dim MyForm As TriArticles

  sel = ""
  Tri = "art_code"
  MyForm = New TriArticles(True, "", Tri, "Mvtex", "", "")
  MyForm.Showmodal()
  If Bsel = True Then
    Cart.text = Codearticle
  Endif
  Cart.SetFocus
  Cart.Select

End

'**********************************************************
'*           Affichage des quantites en stock             *
'**********************************************************
Public Sub Quantite()

  Dim rResult As Result

  Tab = "Fiches_Art"
  With utils
    rResult = Utils.db.Exec("select * from " & Tab & " where art_code = &1", Cart.text)
    If rResult.Available Then
      Stocke = rResult!art_stocke
      Artstock.Text = Format$(rResult!art_qte, "0.000")

    Endif
    If Stocke Then
      Artstock.Text = .Cdec(Decm, Artstock.Text)
      If Val(Artstock.Text) < 1 Then
        Artstock.Background = &D47C0A&
      Else
        Artstock.Background = &HD4D0C8&
      Endif
    Endif
  End With

End

'**********************************************************
'*        Controle de la saisie des quantites             *
'**********************************************************
Public Sub qterecue_LF()

    If qterecue.Value = 0 Then qterecue.Value = 1

End

'***************************************************
'*    Maj de la quantite du stock                              *
'***************************************************
Public Sub Maj_qte(qte As Float)

  Dim QteResult As Result

    QteResult = Utils.db.Edit("Fiches_Art", "art_code = &1", colrcp.Item[0])
    QteResult!art_qte += qte
    QteResult.Update

End

Public Sub ToggleButton2_Click()

  SelectionArt()

End

'******************************************************
'*   On mouvemente le panel Colrcp apres validation   *
'******************************************************
Public Sub Maj_colrcp()

  Dim qte As Float

  If $soption = "B" Or $soption = "T" Then 
    If utils.valart(datej.dte.g, Cart.Text) Then
      Message.Warning("Période CM déjà clôturée \n Veuillez changer la date")
      datej.SetFocus
      Return
    Endif
  Endif
  If IsNull(Cart.Text) Then Return
  If Not Colrcp.Exist(Cart.Text) Then
    Colrcp.Add(Cart.text, Cart.text)
    Colrcp.Item[0] = Cart.text
    Colrcp.Item[1] = Dart.text
    If Entree.Value Then
      Colrcp.Item[2] = qterecue.text
      Colrcp.Item[3] = ""
    Else
      Colrcp.Item[3] = qterecue.text
      Colrcp.Item[2] = ""
    Endif
    Colrcp.Item[4] = Comment.Text
 Else
    If son Then
      Music.Play
    Endif
    Colrcp.MoveTo(Cart.Text)
    If message.Question("Cet article a déjà été saisit ! Voulez-vous ? \n 1- cumuler \n 2- remplacer", "1", "2") = "1" Then
      If IsNull(Colrcp.Item[2]) Then
        qte = utils.totobs([Colrcp.Item[3]]) * -1
      Else
        qte = utils.totobs([Colrcp.Item[2]])
      Endif
      If Entree.Value Then
        qte += qterecue.Value
      Else
        qte -= qterecue.Value
      Endif
      If qte > 0 Then
        qterecue.Value = qte
        colrcp.Item[2] = qterecue.Text
        colrcp.Item[3] = ""
      Endif
      If qte < 0 Then
        qterecue.Value = qte * -1
        colrcp.Item[3] = qterecue.Text
        colrcp.Item[2] = ""
      Endif
      If qte = 0 Then colrcp.Remove(Cart.Text)
    Else
      If Entree.Value Then
        Colrcp.Item[2] = qterecue.text
        Colrcp.Item[3] = ""
      Else
        Colrcp.Item[3] = qterecue.text
        Colrcp.Item[2] = ""
      Endif
    Endif
  Endif

End

'******************************************************
'*       On rappelle un article pour modification     *
'******************************************************
Public Sub Colrcp_Activate()

  Colrcp.MoveFirst
  If Not Colrcp.Available Then
  Else
    Cart.text = Colrcp.Current[0]
    Dart.text = Colrcp.Current[1]
    If Not IsNull(Colrcp.Current[2]) Then
      Entree.Value = True
      qterecue.text = Colrcp.Current[2]
    Else
      Sortie.Value = True
      qterecue.text = Colrcp.Current[3]
    Endif
    art_man()
    Colrcp.Current.Delete
    Colrcp.Sorted = 0
    B = 1
    qterecue.SetFocus
    qterecue.Select
  Endif

End

'******************************************************
'*       On rappelle un article pour suppression      *
'******************************************************
Public Sub Colrcp_Keypress()

  If Key.code = Key.Delete And Colrcp.Count <> 0 Then
    Colrcp.Current.Delete
    Colrcp.Sorted = 0
    B = 1
    Cart.SetFocus
    Cart.Select
    CleanChamps()
  Endif
  
  If Key.Code = Key.Enter Or Key.code = Key.Return Then
     Colrcp_Activate()
  Endif
  
End

'************************************************
'*    On enregistre la saisie dans la table     *
'************************************************
Public Sub Enrgbl()

  Dim qte As Float
  Dim cdd As Calculdroit
  Dim res, resdr As Result
  Dim snumvi, num, numlig As String
  Dim bvign, bdroit As Boolean
  Dim i As Integer
  
  Tab = "Fiches_Mvtexp"
  With Utils
    If Colrcp.Count <> 0 Then
      utils.db.Exec("LOCK TABLES Fiches_Art  WRITE, Fiches_Mvtexp WRITE")
      Colrcp.MoveFirst
      Repeat
        Colrcp.Item.Selected = True
        Utils.db.Exec("INSERT INTO " & Tab & "(code,daterecpt,qtep,qtem,Com) VALUES (&1, &2, &3, &4, &5)", Colrcp.Item[0], datej.dte.G, Colrcp.Item[2], Colrcp.Item[3], Colrcp.Item[4])
        If sortie.Value Then          'maj des stocks
          qte = utils.totobs([Colrcp.Item[3]]) * -1
        Else
          qte = utils.totobs([Colrcp.Item[2]])
        Endif
        Maj_qte(qte)
      Until Colrcp.Movenext()
      utils.db.Exec("UNLOCK TABLES")
    
      'CM si option boissson
      If $soption = "B" Or $soption = "T" Then
        'recherche si droit et si vignette alcool
        Colrcp.MoveFirst
        Repeat
           res = utils.db.Exec("SELECT art_volm,art_deg,suspendu FROM Fiches_Art,Fiches_regie WHERE art_dra=Fiches_regie.code AND art_code=&1 and art_dra IS NOT NULL", Colrcp.Item[0])
           If res.Available Then
             If res.Available Then
               bdroit = True
               If res!suspendu Then bvign = True
             Endif
           Endif
       Until Colrcp.MoveNext()
       'creation du n° d'enregistrement
       Repeat
         i += 1
         num = datej.dte.D & Str(i)
         res = utils.db.Exec("SELECT * FROM Fiches_ligregie WHERE num=&1", num)
        Until Not res.Available
       'écriture du fichier ligregie
       If bdroit Then
         If bvign Then    'recuper le n° de la vigentte ss
           snumvi = atvig()
         Endif
         Colrcp.MoveFirst
         utils.db.Exec("LOCK TABLES Fiches_Art WRITE, Fiches_regie WRITE, Fiches_ligregie WRITE")
         utils.db.Begin
         numlig = "000"
         Repeat
           res = utils.db.Exec("SELECT art_dra,art_volm,art_deg FROM Fiches_Art WHERE art_code=&1 and art_dra IS NOT NULL", Colrcp.Item[0])
           If res.Available Then
             resdr = utils.db.Create("Fiches_ligregie")
             resdr!numlig = numlig
             numlig = Format(Val(numlig) + 1, "000")
             resdr!num = num
             resdr!codeart = Colrcp.Item[0]
             resdr!coderegie = res!art_dra
             resdr!degres = res!art_deg
             If utils.totobs([Colrcp.Item[3]]) <> 0 Then
               qte = utils.totobs([Colrcp.Item[3]])
               resdr!type = "S"
              Else
                qte = utils.totobs([Colrcp.Item[2]])
                resdr!type = "E"
             Endif
             cdd = New Calculdroit(res!art_dra, res!art_volm, res!art_deg, qte)
             resdr!dtmouv = datej.dte.G
             resdr!qte = qte
             resdr!volume = cdd.vol
             resdr!volumeap = cdd.volap
             resdr!montdr = cdd.droit
             resdr!txtvadr = cdd.tauxtvadr
             resdr!montss = cdd.secu
             resdr!txtvass = cdd.tauxtvass
             resdr!vignette = snumvi
             resdr!cloture = False
             resdr!volumess = cdd.volumess
             resdr.Update
           Endif
         Until Colrcp.MoveNext()
         utils.db.Commit
         utils.db.Exec("UNLOCK TABLES")
       Endif
      Endif
      imprim(bvign, bdroit, snumvi)
    Endif
    RazBl()
  End With

End

Private Function atvig() As String
  
  Dim res As Result
  Dim numv As String
  Dim n As Integer
  
  utils.db.Exec("LOCK TABLES Fiches_Parametres WRITE")
  res = utils.db.Exec("SELECT nvign FROM Fiches_Parametres")
  If res.Available Then
    n = utils.totobs([res!nvign]) + 1
    numv = Format(n, "0000000000000000000000000")
    Utils.db.Exec("UPDATE Fiches_Parametres SET nvign=&1", numv)
    utils.db.Exec("UNLOCK TABLES")
    Return numv
  Else
    Message.Error("Probléme dans le paramétrage du numéro vignette")
    utils.db.Exec("UNLOCK TABLES")
    Return Null
  Endif
End

'**************Edition************************

Private Sub imprim(bvign As Boolean, bdroit As Boolean, nvign As String)
  
  Dim rep As Report
  Dim lab As ReportLabel
  Dim hrep As ReportHBox
  Dim vrep As ReportVBox
  Dim cdd As Calculdroit
  Dim bdr As Boolean
  Dim coul As Integer
  Dim res As Result
  Dim qte As Float
  Dim Filename As String
  Dim prt As New Printer
  
  Filename = User.Home & "/tmp/mvtex.pdf"
  prt.OutputFile = Filename
  
  bdr = bvign Or bdroit
  coul = Val("&H" & Hex(Start.R) & Hex(Start.G) & Hex(Start.B))
  rep = New Report
  hrep = New ReportHBox(rep)
  hrep.Height = "5mm"
  hrep.Border = ReportBorder["Top:1pt #000000;Bottom:1pt #000000;Left:1pt #000000;Right:1pt #000000"]
  hrep.Fixed = True
  lab = New ReportLabel(hrep)
  lab.Text = "Liste des mouvements excptionelles du :   " & datej.dte.L
  lab.BackGround = ReportBrush.Color(coul)
  lab.Alignment = Align.Center
  lab.Font = font["Serif,-1"]
  
  hrep = New ReportHBox(rep)
  hrep.Height = "4mm"
  hrep.Border = ReportBorder["Top:1pt #000000;Bottom:1pt #000000;Left:1pt #000000;Right:1pt #000000"]
  hrep.Fixed = True
  
  lab = New ReportLabel(hrep)
  lab.Text = "DATE"
  lab.Font = font["Serif,-3"]
  lab.Border = ReportBorder["Right:1pt #000000"]
  lab.Alignment = Align.Center
  lab.Width = "20mm"
  lab.Height = "3mm"
  lab = New ReportLabel(hrep)
  lab.Text = "N° Article"
  lab.Font = font["Serif,-3"]
  lab.Border = ReportBorder["Right:1pt #000000"]
  lab.Alignment = Align.Center
  lab.Width = "20mm"
  lab.Height = "3mm"
  lab = New ReportLabel(hrep)
  lab.Text = "Libelle"
  lab.Font = font["Serif,-3"]
  lab.Border = ReportBorder["Right:1pt #000000"]
  lab.Alignment = Align.Center
  lab.Expand = True
  lab.Height = "3mm"
  lab = New ReportLabel(hrep)
  lab.Text = "Entrées"
  lab.Font = font["Serif,-3"]
  lab.Border = ReportBorder["Right:1pt #000000"]
  lab.Alignment = Align.Center
  lab.Width = "20mm"
  lab.Height = "3mm"
  lab = New ReportLabel(hrep)
  lab.Text = "Sorties"
  lab.Font = font["Serif,-3"]
  
  lab.Alignment = Align.Center
  lab.Width = "20mm"
  lab.Height = "3mm"
  If bdr Then
    lab.Border = ReportBorder["Right:1pt #000000"]
    lab = New ReportLabel(hrep)
    lab.Text = "Volume"
    lab.Font = font["Serif,-3"]
    lab.Border = ReportBorder["Right:1pt #000000"]
    lab.Alignment = Align.Center
    lab.Width = "20mm"
    lab.Height = "3mm"
    lab = New ReportLabel(hrep)
    lab.Text = "Degré"
    lab.Font = font["Serif,-3"]
    lab.Border = ReportBorder["Right:1pt #000000"]
    lab.Alignment = Align.Center
    lab.Width = "10mm"
    lab.Height = "3mm"
    lab = New ReportLabel(hrep)
    lab.Text = "Alcool Pur"
    lab.Font = font["Serif,-3"]
    lab.Border = ReportBorder["Right:1pt #000000"]
    lab.Alignment = Align.Center
    lab.Width = "20mm"
    lab.Height = "3mm"
  Endif
  
  Colrcp.MoveFirst
  Repeat
    hrep = New ReportHBox(rep)
    hrep.Height = "2mm"
    lab = New ReportLabel(hrep)
    lab.Text = datej.dte.L
    lab.Font = font["Serif,-5"]
    lab.Alignment = Align.Center
    lab.Width = "20mm"
    lab.Height = "2mm"
    lab = New ReportLabel(hrep)
    lab.Text = Colrcp.Item[0]
    lab.Font = font["Serif,-5"]
    lab.Width = "20mm"
    lab.Height = "2mm"
    lab = New ReportLabel(hrep)
    lab.Text = Colrcp.Item[1]
    lab.Font = font["Serif,-5"]
    lab.Width = "20mm"
    lab.Height = "2mm"
    lab = New ReportLabel(hrep)
    lab.Text = Colrcp.Item[2]
    lab.Font = font["Serif,-5"]
    lab.Expand = True
    lab.Height = "2mm"
    lab = New ReportLabel(hrep)
    lab.Text = Colrcp.Item[3]
    lab.Font = font["Serif,-5"]
    lab.Alignment = Align.Right
    lab.Width = "20mm"
    lab.Height = "2mm"
    lab = New ReportLabel(hrep)
    lab.Text = Colrcp.Item[4]
    lab.Font = font["Serif,-5"]
    lab.Alignment = Align.Right
    lab.Width = "20mm"
    lab.Height = "2mm"
    
    If bdr Then
      res = utils.db.Exec("SELECT art_dra,art_volm,art_deg FROM Fiches_Art WHERE art_code=&1 AND art_dra IS NOT NULL", Colrcp.Item[0])
      If res.Available Then
        If IsNull(Colrcp.Item[3]) Then
          qte = utils.totobs(Colrcp.Item[4]) * -1
        Else 
          qte = utils.totobs([Colrcp.Item[3]])
        Endif
        cdd = New Calculdroit(res!art_dra, res!art_volm, res!art_deg, qte)
        lab = New ReportLabel(hrep)
        lab.Text = Format(cdd.vol, "0.00")
        lab.Font = font["Serif,-5"]
        lab.Alignment = Align.Right
        lab.Width = "20mm"
        lab.Height = "2mm"
        lab = New ReportLabel(hrep)
        lab.Text = Format(res!art_deg, "0.00")
        lab.Font = font["Serif,-5"]
        lab.Alignment = Align.Right
        lab.Width = "10mm"
        lab.Height = "2mm"
        lab = New ReportLabel(hrep)
        lab.Text = Format(cdd.volap, "0.00")
        lab.Font = font["Serif,-5"]
        lab.Alignment = Align.Right
        lab.Width = "20mm"
        lab.Height = "2mm"
      Endif
    Endif
  Until Colrcp.MoveNext()
  
   rep.Print(prt)
   Visualiseur.Prog_Editeur(Filename)
  
End

'********************
'* On lance la doc  *
'********************
Public Sub Button4_Click()

  Doc.Open("Mvtexp")

End
